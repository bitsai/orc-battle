(require "engine.scm")
(require "util.scm")

(define-simple-class foe ()
  (health init-value: (randval 10))
  ((dead?)
   (<= health 0))
  ((hit x)
   (swap! health - x)
   (if (dead?)
       (str "You killed the " (type-of (this)) "!\n")
       (str "You hit the " (type-of (this)) " for " x " health!\n")))
  ((show)
   (str "A fierce " (type-of (this))))
  ((attack)
   #!abstract))

(define-simple-class orc (foe)
  (club-level init-value: (randval 8))
  ((show)
   (str "A wicked orc with a level " club-level " club"))
  ((attack)
   (let ((x (randval club-level)))
     (swap! *player-health* - x)
     (str "An orc clubs you and knocks off " x " of your health!\n"))))

(define-simple-class hydra (foe)
  ((show)
   (str "A malicious hydra with " health " heads"))
  ((hit x)
   (swap! health - x)
   (if (invoke-special foe (this) 'dead?)
       "The fully decapitated hydra falls to the floor!\n"
       (str "You knock off " x " of the hydra's heads!\n")))
  ((attack)
   (let ((x (randval (quotient health 2))))
     (swap! health inc)
     (swap! *player-health* - x)
     (str "A hydra attacks you with " x " of its heads!\n"
	  "It also grows back 1 more head!\n"))))

(define-simple-class slime (foe)
  (sliminess init-value: (randval 5))
  ((show)
   (str "A slime with a sliminess of " sliminess))
  ((attack)
   (let ((x (randval sliminess)))
     (swap! *player-agility* - x)
     (if (zero? (rand-int 2))
	 (begin (swap! *player-health* dec)
		(str "A slime wraps your legs and takes away " x " agility!\n"
		     "It also squirts your face, taking away 1 health!\n"))
	 (str "A slime wraps your legs and takes away " x " agility!\n")))))

(define-simple-class brigand (foe)
  ((attack)
   (let ((x (max *player-health* *player-agility* *player-strength*)))
     (cond ((= x *player-health*)
	    (swap! *player-health* - 2)
	    "A brigand hits with his slingshot for 2 health!\n")
	   ((= x *player-agility*)
	    (swap! *player-agility* - 2)
	    "A brigand whips your leg for 2 agility!\n")
	   ((= x *player-strength*)
	    (swap! *player-strength* - 2)
	    "A brigand whips your arm for 2 strength!\n")))))
