(require 'list-lib)
(require "battle.scm")
(require "util.scm")

;; Global variables
(define *player-health* #f)
(define *player-agility* #f)
(define *player-strength* #f)

(define *monsters* '())
(define *monster-builders* '())
(define *monster-num* 12)

(define *input-state* #f)
(define *attacks-left* #f)
(define *hits-left* #f)
(define *hit-strength* #f)

;; Main game functions
(define (new-game)
  (init-monsters)
  (init-player)
  (new-turn))

(define (new-turn)
  (show-player)
  (set! *attacks-left* (inc (quotient (max 0 *player-agility*) 15)))
  (pick-attack))

(define (process-input input)
  (case *input-state*
    ((pick-attack) (process-attack input))
    ((pick-target) (process-target input))))

(define (end-attack)
  (swap! *attacks-left* dec)
  (if (or (zero? *attacks-left*) (monsters-dead?))
      (end-turn)
      (pick-attack)))

(define (end-turn)
  (dolist (m ::monster (remove monster-dead? *monsters*))
          (m:attack))
  (if (or (player-dead?) (monsters-dead?))
      (end-game)
      (new-turn)))

(define (end-game)
  (when (player-dead?)
    (output "You have been killed. Game over.\n"))
  (when (monsters-dead?)
    (output "Congratulations! You have vanquished all of your foes.\n")))

;; Player management functions
(define (init-player)
  (set! *player-health* 30)
  (set! *player-agility* 30)
  (set! *player-strength* 30))

(define (player-dead?)
  (<= *player-health* 0))

(define (show-player)
  (output "You are a mystic monk with "
          *player-health* " health, "
          *player-agility* " agility, and "
          *player-strength* " strength.\n"))

(define (pick-attack)
  (show-monsters)
  (output "Attack style: [k]i strike [d]ual strike [f]lurry of blows\n")
  (set! *input-state* 'pick-attack))

(define (process-attack input)
  (case input
    ((k) (let ((x (+ 2 (randval (quotient *player-strength* 2)))))
	   (output "Your ki strike has a strength of " x ".\n")
           (set! *hits-left* 1)
	   (set! *hit-strength* x)
           (pick-target)))
    ((d) (let ((x (randval (quotient *player-strength* 6))))
	   (output "Your dual strike has a strength of " x ".\n")
	   (set! *hits-left* 2)
           (set! *hit-strength* x)
           (pick-target)))
    ((f) (begin (dotimes (_ (inc (randval (quotient *player-strength* 3))))
			 (unless (monsters-dead?)
                           ((random-monster):hit 1)))
		(end-attack)))
    (else (output "That is not a valid attack.\n"))))

(define (pick-target)
  (output "Monster #:\n")
  (set! *input-state* 'pick-target))

(define (process-target input)
  (let ((m (pick-monster input)))
    (unless (eqv? m #!null)
      (m:hit *hit-strength*)
      (swap! *hits-left* dec)
      (if (or (zero? *hits-left*) (monsters-dead?))
          (end-attack)
          (pick-target)))))

;; Helper functions for player attacks
(define (random-monster) ::monster
  (let ((m (rand-nth *monsters*)))
    (if (monster-dead? m)
	(random-monster)
	m)))

(define (pick-monster x) ::monster
  (if (not (and (integer? x) (>= x 1) (<= x *monster-num*)))
      (begin (output "That is not a valid monster number.\n")
	     #!null)
      (let ((m (list-ref *monsters* (dec x))))
	(if (monster-dead? m)
	    (begin (output "That monster is already dead.\n")
		   #!null)
	    m))))

;; Monster management functions
(define (init-monsters)
  (set! *monster-builders* (list orc hydra slime brigand))
  (let ((init-rand-monster (lambda (_) ((rand-nth *monster-builders*)))))
    (set! *monsters* (list-tabulate *monster-num* init-rand-monster))))

(define (monster-dead? m ::monster)
  (<= m:health 0))

(define (monsters-dead?)
  (every monster-dead? *monsters*))

(define (show-monsters)
  (output "Your foes:\n")
  (dolist (x (iota *monster-num*))
	  (let ((m ::monster (list-ref *monsters* x)))
	    (output "  " (inc x) ". ")
	    (if (monster-dead? m)
		(output "**dead**\n")
		(output "(Health = " m:health ") " (m:show) "\n")))))

;; The monsters
(define-simple-class monster ()
  (health)
  ((*init*)
   (set! health (randval 10)))
  ((hit x)
   (swap! health - x)
   (if (monster-dead? (this))
       (output "You killed the " (type-of (this)) "!\n")
       (output "You hit the " (type-of (this)) ", knocking off " x " health points!\n")))
  ((show)
   (str "A fierce " (type-of (this))))
  ((attack)
   #!abstract))

(define-simple-class orc (monster)
  (club-level)
  ((*init*)
   (invoke-special monster (this) '*init*)
   (set! club-level (randval 8)))
  ((show)
   (str "A wicked orc with a level " club-level " club"))
  ((attack)
   (let ((x (randval club-level)))
     (output "An orc swings his club at you and knocks off " x " of your health points!\n")
     (swap! *player-health* - x))))

(define-simple-class hydra (monster)
  ((show)
   (str "A malicious hydra with " health " heads"))
  ((hit x)
   (swap! health - x)
   (if (monster-dead? (this))
       (output "The corpse of the fully decapitated and decapacitated hydra falls to the floor!\n")
       (output "You knock off " x " of the hydra's heads!\n")))
  ((attack)
   (let ((x (randval (quotient health 2))))
     (output "A hydra attacks you with " x " of its heads! It also grows back 1 more head!\n")
     (swap! health inc)
     (swap! *player-health* - x))))

(define-simple-class slime (monster)
  (sliminess)
  ((*init*)
   (invoke-special monster (this) '*init*)
   (set! sliminess (randval 5)))
  ((show)
   (str "A slime with a sliminess of " sliminess))
  ((attack)
   (let ((x (randval sliminess)))
     (output "A slime wraps around your legs and decreases your agility by " x "!\n")
     (swap! *player-agility* - x)
     (when (zero? (rand-int 2))
       (output "It also squirts in your face, taking away 1 health point!\n")
       (swap! *player-health* dec)))))

(define-simple-class brigand (monster)
  ((attack)
   (let ((x (max *player-health* *player-agility* *player-strength*)))
     (cond ((= x *player-health*)
            (output "A brigand hits you with his slingshot, taking off 2 health points!\n")
	    (swap! *player-health* - 2))
	   ((= x *player-agility*)
            (output "A brigand catches your leg with his whip, taking off 2 agility points!\n")
	    (swap! *player-agility* - 2))
	   ((= x *player-strength*)
            (output "A brigand cuts your arm with his whip, taking off 2 strength points!\n")
	    (swap! *player-strength* - 2))))))
